# vim:ts=3:et:ft=tcsh
#
# tcsh 6 config
# joshua sein <jcs@jcs.org>
#

# environment variables
setenv BLOCKSIZE 1k
setenv CVS_RSH /usr/bin/ssh
setenv IRCNAME "*Unknown*"

# ow my security
setenv MYSQL_HISTFILE /dev/null

# pass through to bash in case it somehow gets invoked
setenv HISTFILE

# let my ~/bins override, find other bins quickly
setenv PATH ~/bin:/bin:/usr/bin:/sbin:/usr/sbin:/usr/local/bin:/usr/local/sbin:/usr/X11R6/bin

# on non-interactive shells, just exit here to speed things up
if ($?USER == 0 || $?prompt == 0 || $tty == "") then
	exit
endif

# i'm too lazy to type these out
alias ccom 'cvs -q diff -upRN \!* || (echo; echo enter to commit; sh -c read && cvs com \!*)'
alias cdu 'cvs -q diff -upRN'
alias cp 'cp -i'
alias hg 'history | grep '
alias ll 'ls -alF'
alias lo 'logout'
alias ls 'ls -aF'
if ("$OSTYPE" == "linux") then
	alias ls 'ls -aFv'
endif
alias manfile 'groff -man -Tascii \!* | less'
alias mv 'mv -i'
alias offline_mutt 'mutt -R -F ~/.muttrc.offline'
alias ph 'ps auwwx | head'
alias pg 'ps auwwx | grep -i -e ^USER -e '
alias refetch 'cvs -q up -PACd'
alias telnet 'telnet -K'
alias tin 'tin -arQ'
alias u 'cvs -q up -PAd'

# for openbsd ports
alias remake 'cd ../../; rm w-*/.build*; make; cd -'

if ("$OSTYPE" == "darwin") then
   # mac os'isms
   alias ldd 'otool -L'
   alias sha1 'openssl dgst -sha1'

   # pre-lion uses dscacheutil
   alias dnsflush 'dscacheutil -flushcache; sudo killall -HUP mDNSResponder'

else if ("$OSTYPE" == "OpenBSD") then
   setenv PKG_PATH ftp://mirror.planetunix.net/pub/OpenBSD/snapshots/packages/`arch -s`/
   alias pkg_replace 'sudo pkg_add -r -F installed'
   alias watchbw 'netstat -w1 -b -I'
endif

# where am i
alias publicip 'curl http://ifconfig.me'

# when i say vi i mean vim (if it's installed)
if (-x "`which vim`") then
   alias vi 'vim'
   alias view 'vim -R'
   setenv EDITOR `which vim`
else
   setenv EDITOR /usr/bin/vi
endif

# keybindings
bindkey ^[[1~ beginning-of-line      # home key
bindkey ^[[4~ end-of-line            # end key
bindkey ^T load-average              # siginfo
bindkey ^W backward-delete-word      # just a word, thanks

# i never need to look in these
set fignore="CVS"

# super duper tab completion
set noglob
  # jobs
  complete {fg,bg,stop}  c/%/j/ p/1/"(%)"//

  # settings
  complete {,un}alias  'p/1/a/' 'p/2/c/'
  complete bindkey 'C/*/b/'
  complete env 'c/*=/f/' 'p/1/e/=/' 'p/2/c/'
  complete {,un}limit c/-/"(h)"/ n/*/l/
  complete {,un}setenv   'p/1/e/' 'c/*:/f/'
  complete {,un}set   'c/*=/f/' 'p/1/s/=' 'n/=/f/'

  # only cd to dirs
  complete cd p/1/d/

  # show bins
  complete {man,which,where,whereis}  'C/*/c/'

  # let me tab complete ssh hosts 
  set hosts=""
  foreach f ($HOME/.hosts /usr/local/etc/csh.hosts $HOME/.rhosts /etc/hosts.equiv)
    if ( -r $f ) then
      set hosts = ($hosts `grep -v "+" $f | grep -E -v "^#" | tr -s " " " " | cut -f 1`)
    endif
  end
  if ( -r $HOME/.ssh/known_hosts ) then
    set f=`cat $HOME/.ssh/known_hosts | cut -f 1 -d \ ` >& /dev/null
    set f=`cat $HOME/.ssh/known_hosts | cut -f 1 -d \ | sed -e 's/,/ /g'` >& /dev/null
    set hosts=($hosts $f)
  endif
  unset f
  complete ssh p/1/\$hosts/ c/-/"(l n)"/   n/-l/u/ N/-l/c/ n/-/c/ p/2/c/ p/*/f/
  complete {ping,telnet,traceroute} n/*/\$hosts/

  # some commit git sub-commands
  set gitc="checkout commit diff log merge pull push remote stash"
  complete git "p/1/($gitc)/"
  unset gitc
unset noglob 
set correct=all

# options
set autocorrect                      # fix typos
set autolist                         # don't make me hit ctrl+d
set autologout=0                     # maybe i'm just taking a nap
set correct=none                     # don't assume anything
set history=500                      # just in case
unset ignoreeof                      # when i control+d, i want to logout
set listjobs=long                    # shoe me pids
set notify                           # curious george
set noclobber                        # halp me
set printexitvalue                   # i want to know if something went wrong
set prompt='%n@%m:%~%# '             # yeah
set promptchars='>#'                 # mac os uses % instead of >, suk
set rmstar                           # recycle bin?
set watch=(0 any any)                # i see all
set who="%B%n%b %a %l at %@"         # format a bit

# etc
limit coredumpsize 0                 # don't know why you'd want anything else
umask 022                            # be nice

# load any local aliases and machine-specific things
if (-f ~/.tcshrc.local) then
   source ~/.tcshrc.local
endif

if ("$OSTYPE" != "linux") then
	# siginfo
	stty status '^T'
endif
